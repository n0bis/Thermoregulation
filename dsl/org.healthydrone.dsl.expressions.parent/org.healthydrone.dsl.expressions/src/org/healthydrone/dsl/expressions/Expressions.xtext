
grammar org.healthydrone.dsl.expressions.Expressions with org.eclipse.xtext.common.Terminals

generate expressions "http://www.healthydrone.org/dsl/expressions/Expressions"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

RulesModel:
	models += Model*
;

Model:
	'Rule' value=STRING
	rules += Rule*
	'end'
;

Rule:
	expression=Expression (actions+=Action*)
;

enum Color:
    RED = 'red' | GREEN = 'green' | BLUE = 'blue'
;

Action:
	 'action' name=AllowedActions value=Color
;

Expression: Or;

Or returns Expression:
	And ({Or.left=current} "||" right=And)*
;

And returns Expression:
	Equality ({And.left=current} "&&" right=Equality)*
;

Equality returns Expression: 
	Comparison (
		{Equality.left=current} op=("=="|"!=")
       	right=Comparison
    )*
;

Comparison returns Expression: 
	Atomic (
		{Comparison.left=current} op=(">="|"<="|">"|"<")
   		right=Atomic
    )*
;

Atomic returns Expression:
	{IntConstant} value=SignedInt | {StringConstant} value=('temperature' | 'humidity')
;

SignedInt returns ecore::EInt: '-'? INT;

AllowedActions:
	'LEDblink' | 'log'
;